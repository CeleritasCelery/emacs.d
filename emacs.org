#+TITLE: Emacs Literate Configuration
#+AUTHOR: Troy Hinckley
#+PROPERTY: header-args :tangle yes


* Configuration
:PROPERTIES:
:VISIBILITY: children
:END:
** Design Choices
I have some basic design principles that I am trying to keep
consistent through all of my config.

*** namespace
I am using =$= as my personal namespace. I like it because it reminds
me of perl, it is really simple, and this is elisp, so I can make my
namespace whatever I want.

*** lambdas
the easiest way add a simple wrapper function to a hook or advice is
to use lambdas. However that has the problem of making them very hard
to inspect and remove. It is better to use named functions. However it
can be confusing if a function is only used as a named lambda or if it
is being used elsewhere. Therefore I will put named functions (using
=defun=) inside of =add-hook= or =advice-add=. This allows me to
clearly associate the function with the purpose but also avoids the
confusion surrounding anonymous functions.

*** overrides
Never override a function when an advice will do. when I actually do
need to override a function, I will try to use the package =el-patch=
to make it maintainable.


** Emacs Initialization

*** lexical bindings

make this file lexically bound
#+BEGIN_SRC emacs-lisp
  ;;; init.el --- personal emacs config file -*- lexical-binding: t -*-
#+END_SRC

*** Personal Information
Let's set some variables with basic user information.

#+BEGIN_SRC emacs-lisp
  (setq user-full-name "Troy Hinckley"
        user-mail-address "t.macman@gmail.com")
#+END_SRC

*** customization

stolen from https://github.com/abo-abo/oremacs/blob/github/init.el
makes sure that we are using the correct setter for all variables
#+BEGIN_SRC emacs-lisp
(defmacro csetq (variable value)
  `(funcall (or (get ',variable 'custom-set) 'set-default) ',variable ,value))
#+END_SRC

*** Settings

#+BEGIN_SRC emacs-lisp
  (defvar $leader-key "SPC"
    "leader key used to quicky access commands.")

  (defvar $mm-leader-key ","
    "leader key for major mode specific commands")
#+END_SRC

don't use menu or scroll bars. We want the UI to be as clean as
possible.
#+BEGIN_SRC emacs-lisp
  (tool-bar-mode -1)
  (menu-bar-mode -1)
  (scroll-bar-mode -1)
  (tooltip-mode -1)
  (setq inhibit-startup-screen t)
  (toggle-frame-fullscreen)
  (add-hook 'focus-in-hook 'redraw-display)
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (electric-pair-mode)
#+END_SRC

use =y= and =n= instead of =yes= and =no=
#+BEGIN_SRC emacs-lisp
  (defalias 'yes-or-no-p 'y-or-n-p)
#+END_SRC

move the custom file to its own loaction so it does not clutter us.

#+BEGIN_SRC emacs-lisp
  (setq custom-file (expand-file-name "custom.el" user-emacs-directory))
  (load-file custom-file)
#+END_SRC

*** tangling
    we want to tangle on exit so that it will make startup time
    quicker.
#+BEGIN_SRC emacs-lisp
  (defun $tangle-init-file ()
    "tangle my emacs org file before closing to make startup faster."
    (interactive)
    (require 'ob-tangle)
    (org-babel-tangle-file (expand-file-name "emacs.org" user-emacs-directory)
                           (expand-file-name "emacs.el" user-emacs-directory)))

  (add-hook 'kill-emacs-hook '$tangle-init-file)
#+END_SRC

    by setting the initial mode we won't have to load lisp at startup
#+BEGIN_SRC emacs-lisp
  (setq initial-major-mode 'fundamental-mode)
#+END_SRC
* Packages
:PROPERTIES:
:VISIBILITY: children
:END:
** Package Manager
We are going to use =straight.el= as package manager because it allows
us to easily maintain private repos of packages. This is still slower
then using package.el, but I think it is worth it. below is the
bootstrap code to download the package manager and run it.

#+BEGIN_SRC emacs-lisp
  (setq straight-recipes-gnu-elpa-url  "http://git.savannah.gnu.org/git/emacs/elpa.git")
  (let ((bootstrap-file
         (expand-file-name "straight/repos/straight.el/bootstrap.el" user-emacs-directory))
        (bootstrap-version 4))
    (unless (file-exists-p bootstrap-file)
      (with-current-buffer
          (url-retrieve-synchronously
           "https://raw.githubusercontent.com/raxod502/straight.el/develop/install.el"
           'silent 'inhibit-cookies)
        (goto-char (point-max))
        (eval-print-last-sexp)))
    (load bootstrap-file nil 'nomessage))
#+END_SRC

** packages

=use-package= is a macro that allows to easily define and load other
packages.  It is the first thing we want to install. I am using my own
fork of the package that contains more sane defaults for the =:hook=
keyword (essentially it automatically adds the =-mode= suffix).

*** use-package
#+BEGIN_SRC emacs-lisp
  (straight-use-package
   '(use-package
      :type git :host github :repo "CeleritasCelery/use-package"
      :upstream (:host github
                       :repo "jwiegley/use-package")))
#+END_SRC

Setting =straight-use-package-by-default= to =t= we can ensure that
=use-pacakge= will use =straight.el= to install missing packages for
us. Always defer will guarantee that all packages are autoloaded
unless explicitly stated otherwise.
#+BEGIN_SRC emacs-lisp
  (setq straight-use-package-by-default t
        use-package-always-defer t)
#+END_SRC

*** delight
This allows us to remove mode-line lighters
#+BEGIN_SRC emacs-lisp
  (use-package delight)
#+END_SRC

*** emacs startup profiler
The "Emacs Startup profiler". This allows us to see what parts of the
config are most heavily impacting start up time. you can't optimize
until you have good info
#+BEGIN_SRC emacs-lisp
  (use-package esup)
#+END_SRC

** bootstrap
packages that are used to setup my emacs enviroment. They are
needed as dependaceies for other packages later on.

*** general package
This package lets us use "SPC" as leader key for other keybindings. It
also provides a lot of helper functions to make binding keys easier
and smarter.
#+BEGIN_SRC emacs-lisp
  (use-package general
    :demand t
    :config
    (general-create-definer $leader-set-key
      :prefix $leader-key
      :states 'motion
      :keymaps 'override)
    (general-create-definer $leader-local-set-key
      :prefix $mm-leader-key
      :states 'motion))
#+END_SRC

Use tab for indentation and symbol completion
#+BEGIN_SRC emacs-lisp
  (general-define-key :states '(insert normal visual)
                      "TAB" 'indent-for-tab-command)
  (setq tab-always-indent 'complete)
#+END_SRC

*** no littering
make sure that third party files cannot leave conifg and save files
all over the place
#+BEGIN_SRC emacs-lisp
  (use-package no-littering
    :demand t)
#+END_SRC

*** save hist
save minibuffer history between sessions.
#+BEGIN_SRC emacs-lisp
  (use-package savehist
    :straight nil
    :defer 1
    :config
    (savehist-mode))
#+END_SRC

*** hyrda
hydra provides repeatable keybindings to quickly execute multiple
commands
#+BEGIN_SRC emacs-lisp
  (use-package hydra)
#+END_SRC

*** el-patch
#+BEGIN_SRC emacs-lisp
  (use-package el-patch)
#+END_SRC
** UI
packages that are used to improve the visuals and interface for Emacs

*** highlight line

highlight the current line with a background face
#+BEGIN_SRC emacs-lisp
  (use-package hl-line
    :demand t
    :config
    (global-hl-line-mode))
#+END_SRC

*** keybings
use =key-freq= to see your key usage.
#+BEGIN_SRC emacs-lisp
  (use-package keyfreq
    :defer 1
    :init
    (setq keyfreq-excluded-commands '(org-self-insert-command self-insert-command))
    :config
    (keyfreq-mode)
    (keyfreq-autosave-mode))
#+END_SRC

*** font
Setup the font that I want to use. Hasklig is a fork of /Source Code
Pro/ that contains ligatures.
#+BEGIN_SRC emacs-lisp
(set-face-attribute 'default nil
                    :family "Hasklig"
                    :height 120)
#+END_SRC

use a hydra to scale the text size
#+BEGIN_SRC emacs-lisp
  (defhydra text-scale (:hint nil)
    "
  Text Scale
    [_+_/_=_] scale up [_-_] scale down [_0_] reset font [_q_] quit
  "
    ("+" text-scale-increase)
    ("=" text-scale-increase)
    ("-" text-scale-decrease)
    ("0" (text-scale-set 0) :exit t)
    ("q" nil :exit t))
  ($leader-set-key
    "z" '(:ignore t :wk "util")
    "zs" 'text-scale/body)
#+END_SRC

*** ligatures
liguatures use a custom symbol to represent two or more
characters. The haskling font is required to make these work. Another
option would be FiraCode, but I am pretty happy with Hasklig for now.
#+BEGIN_SRC emacs-lisp

  (setq prettify-symbols-unprettify-at-point t)

  (defvar $prog-prettify-symbols-alist
    `(("&&"  . (?\s (Br . Bl) ?\s (Br . Br) ,(decode-char 'ucs #XE100)))
      ("||"  . (?\s (Br . Bl) ?\s (Br . Br) ,(decode-char 'ucs #XE104)))
      ("::"  . (?\s (Br . Bl) ?\s (Br . Br) ,(decode-char 'ucs #XE106)))
      ("=="  . (?\s (Br . Bl) ?\s (Br . Br) ,(decode-char 'ucs #XE107)))
      ("=>"  . (?\s (Br . Bl) ?\s (Br . Br) ,(decode-char 'ucs #XE10A)))
      (">>"  . (?\s (Br . Bl) ?\s (Br . Br) ,(decode-char 'ucs #XE10D)))
      ("->"  . (?\s (Br . Bl) ?\s (Br . Br) ,(decode-char 'ucs #XE112)))
      ("<<"  . (?\s (Br . Bl) ?\s (Br . Br) ,(decode-char 'ucs #XE11C)))
      (".."  . (?\s (Br . Bl) ?\s (Br . Br) ,(decode-char 'ucs #XE11F)))
      ("++"  . (?\s (Br . Bl) ?\s (Br . Br) ,(decode-char 'ucs #XE121)))
      ("!="  . (?\s (Br . Bl) ?\s (Br . Br) ,(decode-char 'ucs #XE123)))
      ("..." . (?\s (Br . Bl) ?\s (Br . Bl) ?\s (Br . Br) ,(decode-char 'ucs #XE120)))
      ("->>" . (?\s (Br . Bl) ?\s (Br . Bl) ?\s (Br . Br) ,(decode-char 'ucs #XE126)))
      ("=<<" . (?\s (Br . Bl) ?\s (Br . Bl) ?\s (Br . Br) ,(decode-char 'ucs #XE10B)))
      (".=<<" . (?· (Br . Bl) ?\s (Br . Bl) ?\s (Br . Bl) ?\s (Br . Br)  ,(decode-char 'ucs #XE10B)))
      (".="  . (?· (Br . Bl) ?=))
      ("<="  . (?\s (Br . Bl) ?\s (Bc . Bc) ?< (Bc . Bc) ?_))
      (">="  . (?\s (Br . Bl) ?\s (Bc . Bc) ?> (Bc . Bc) ?_))))

  (defun $prettify-base-symbols ()
    "enable hasklig ligatures"
    (interactive)
    (dolist (symbol $prog-prettify-symbols-alist)
      (add-to-list 'prettify-symbols-alist symbol))
    (prettify-symbols-mode))

  (add-hook 'prog-mode-hook '$prettify-base-symbols)
#+END_SRC

*** vnc size
change the size of the VNC to match the size of the monitor that I am
using. Since I always run my VNC fullscreen having the VNC resolution
not match the resolution of my monitor results in weird text sizes.
#+BEGIN_SRC emacs-lisp
  (defun vnc-resize (size)
    (shell-command (concat "xrandr --size " size)))

  (defhydra vnc-resize (:columns 2 :exit t)
    "VNC Resize"
    ("l" (vnc-resize "1920x1200") "single monitor (large)")
    ("m" (vnc-resize "1536x864") "mobile")
    ("w" (vnc-resize "3840x1200") "double monitor (wide)")
    ("s" (vnc-resize "1920x1080") "short")
    ("r" (vnc-resize "1600x1200") "square"))
  ($leader-set-key
    "zn"  'vnc-resize/body)
#+END_SRC

*** themes
Creating a collection of themes that I like. I can use
=helm-themes= to switch between them. Some of these themes do
not have all faces that I would like, so When I get some time I will
modify them.
#+BEGIN_SRC emacs-lisp
  (use-package challenger-deep-theme)
  (use-package gruvbox-theme)
  (use-package darktooth-theme)
  (use-package spacemacs-theme)
  (use-package dracula-theme)
  (use-package moe-theme
    :init
    (add-to-list 'custom-theme-load-path
                 "~/.emacs.d/straight/build/moe-theme/"))
  (use-package doom-themes)
  (use-package solarized-theme)
  (use-package color-theme-sanityinc-tomorrow)
  (use-package noctilux-theme)
  (use-package flatland-theme)
  (use-package monokai-theme)

  (load-theme 'doom-one t)
#+END_SRC

*** modeline
I am still struggling to find a modeline I really like. I have settled
on doom for now, but there are some segments that I would like to add
such as compilation exit status. My biggest beef with doom-modeline is
it does not play nice non "doom" themes. I think powerlines are cool
looking, but I am honestly becomeling less of a fan of them. I may
just end up making my own modeline from scratch.
#+BEGIN_SRC emacs-lisp :tangle ~/.emacs.d/ignore.el
  (use-package smart-mode-line
    :demand t
    :config
    (sml/setup))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (use-package doom-modeline
    :straight
    (doom-modeline :type git :host github :repo "seagle0128/doom-modeline")
    :hook (after-init . doom-modeline-init)
    :config
    (setq eldoc-eval-preferred-function 'eval-expression))
#+END_SRC

#+BEGIN_SRC emacs-lisp :tangle ~/.emacs.d/ignore.el
  (use-package telephone-line
    :init
    (setq telephone-line-primary-left-separator 'telephone-line-cubed-left
          telephone-line-secondary-left-separator 'telephone-line-cubed-hollow-left
          telephone-line-primary-right-separator 'telephone-line-cubed-right
          telephone-line-secondary-right-separator 'telephone-line-cubed-hollow-right
          telephone-line-lhs '((evil   . (telephone-line-evil-tag-segment))
                               (accent . (telephone-line-vc-segment
                                          telephone-line-process-segment))
                               (nil    . (telephone-line-buffer-segment))))
    (telephone-line-mode))
#+END_SRC

*** which key
which key is an awesome package that shows me the key I can press
after choosing a prefix key.
#+BEGIN_SRC emacs-lisp
  (use-package which-key
    :delight
    :demand t
    :init
    (setq which-key-idle-delay 0.5
          which-key-idle-secondary-delay 0.1
          which-key-allow-evil-operators t)
    :config
    (which-key-mode))
#+END_SRC

*** ace window
This is a window managment package that I am testing out. it works
pretty well, but I have a couple of things I would like to change.
1. there is no good way to operate on the current window, you have to
   knows its letter first, which is not always easy. my idea is that
   the capital of action would operate on the current window. For
   example =SPC wX= would delete the current window. This would take a
   fair amount of work to change the package however. Or at least so I
   think, I have not actually looked at it yet. I want to wait for
   while to make this change so that I can get the muscle memory down
   and see if that makes this easier with this package.
2. This package will split the window but leave the cursor in the old
   window. I relalize this is just a little thing. but it is very
   unintuitive for me and I have to think about it every time.
#+BEGIN_SRC emacs-lisp
  (use-package ace-window
    :init
    (setq aw-dispatch-always t
          aw-background nil)
    ($leader-set-key
      "w" 'ace-window)
    :config
    (add-to-list 'aw-dispatch-alist '(?w $toggle-maximize-window))
    (add-to-list 'aw-dispatch-alist '(?d aw-delete-window "delete window"))
    (add-to-list 'aw-dispatch-alist '(?s aw-split-window-horz "Split Horz window")))

  ;; from https://gist.github.com/3402786
  (defun $toggle-maximize-window ()
    "Maximize buffer"
    (interactive)
    (if (and (= 1 (length (window-list)))
             (assoc ?_ register-alist))
        (jump-to-register ?_)
      (progn
        (window-configuration-to-register ?_)
        (delete-other-windows))))
#+END_SRC

*** shackle
this is a window managment package that is very minimalistic. I am
going to use it until I find a case where it won't work, then I might
try a more powerful package like popwin.
#+BEGIN_SRC emacs-lisp
  (use-package shackle
    :demand t
    :config
    (add-to-list 'shackle-rules '("*Help*" :select t :align below))
    (add-to-list 'shackle-rules '("*ielm*" :same nil :popup t :select t))
    (shackle-mode))
#+END_SRC

*** buffers
A collection of functions stolen from Spacemacs that allows me to more
easily manipulate files, buffers, and windows.

#+BEGIN_SRC emacs-lisp
  (defun $alternate-buffer (&optional window)
    "Switch back and forth between current and last buffer in the
  current window."
    (interactive)
    (let ((current-buffer (window-buffer window))
          (buffer-predicate
           (frame-parameter (window-frame window) 'buffer-predicate)))
      ;; switch to first buffer previously shown in this window that matches
      ;; frame-parameter `buffer-predicate'
      (switch-to-buffer
       (or (cl-find-if (lambda (buffer)
                         (and (not (eq buffer current-buffer))
                              (or (null buffer-predicate)
                                  (funcall buffer-predicate buffer))))
                       (mapcar #'car (window-prev-buffers window)))
           ;; `other-buffer' honors `buffer-predicate' so no need to filter
           (other-buffer current-buffer t)))))

  (defun $quit-emacs ()
    "save buffers and quit"
    (interactive)
    (save-some-buffers)
    (kill-emacs))

  (defun $open-scratch-buffer ()
    "open the scratch buffer"
    (interactive)
    (set-window-buffer (selected-window)
                       (get-buffer-create "*scratch*")))

  (defun $show-and-copy-buffer-filename (arg)
    "Show and copy the full path to the current file in the minibuffer."
    (interactive "P")
    ;; list-buffers-directory is the variable set in dired buffers
    (let ((file-name (or (buffer-file-name)
                         list-buffers-directory
                         default-directory)))
      (if file-name
          (message (kill-new (if (null arg)
                                 (file-truename file-name)
                               file-name)))
        (error "Buffer not visiting a file"))))

  ($leader-set-key
    "TAB" '$alternate-buffer
    "fy" '$show-and-copy-buffer-filename
    "b" '(:ignore t :wk "buffers")
    "bs" '$open-scratch-buffer
    "q" '(:ignore t :wk "quit")
    "qq" '$quit-emacs)
#+END_SRC

quick movement hydra.

#+BEGIN_SRC emacs-lisp
  (defhydra buffer-nav (:exit nil)
    "move quickly through recent buffers"
    ("p" previous-buffer "prev")
    ("N" previous-buffer "prev")
    ("n" next-buffer "next"))

  ($leader-set-key
    "bp" 'buffer-nav/previous-buffer
    "bn" 'buffer-nav/next-buffer)
#+END_SRC

*** window
switch back to minibuffer when it is active.

#+BEGIN_SRC emacs-lisp
  (defun $switch-to-minibuffer-window ()
    "switch to minibuffer window (if active)"
    (interactive)
    (when (active-minibuffer-window)
      (select-frame-set-input-focus (window-frame (active-minibuffer-window)))
      (select-window (active-minibuffer-window))))

  ($leader-set-key
    "bm" '$switch-to-minibuffer-window)
#+END_SRC

*** winum
#+BEGIN_SRC emacs-lisp
  (use-package winum
    :defer 1
    :init
    (dolist (num (number-sequence 0 9))
      (let ((str (number-to-string num)))
        (eval `($leader-set-key
                 ,str (intern (concat "winum-select-window-" ,str))))))
    :config
    (winum-mode))
#+END_SRC

*** helpful
helpful provides better information about variables and
functions. only tweak we need to make is let the window close with q
#+BEGIN_SRC emacs-lisp
  (use-package helpful
    :init
    ($leader-set-key
      "h" '(:ignore t :wk "help")
      "hd" '(:ignore t :wk "describe")
      "hdf" 'helpful-callable
      "hdv" 'helpful-variable
      "hdk" 'helpful-key)
    (general-define-key
     :keymaps 'helpful-mode-map
     :states 'normal
     "q" 'quit-window))
#+END_SRC

we are going to add helpful to the completing read handler for
helm. This will let us preview the variable with TAB.
#+BEGIN_SRC emacs-lisp
  (dolist (help-fn '(helpful-variable
                     helpful-function
                     helpful-macro
                     helpful-key
                     helpful-callable))
    (map-put helm-completing-read-handlers-alist help-fn 'helm-completing-read-symbols))
#+END_SRC
*** persp-mode
persp-mode is layout managment package that provides way more
functionality then I want. All I really are about is having named
groups of eyebrowse window configs. I could probably drop persp mode
and create a wrapper around =eyebrowse= that could group the window
configs under a name. I would use only eyebrowse, but then I would
have to try to remember what windows go to what project, and that can
get a little confusing. Also I have created some fuctions that make
shell-pop perspective local, and I really like that feature.
#+BEGIN_SRC emacs-lisp
  (use-package persp-mode
    :init
    (setq persp-auto-save-opt 0)
    (defhydra persp (:exit t :pre (persp-mode))
      "Perspective"
      ("l" persp-switch "switch")
      ("n" persp-next "next" :exit nil)
      ("p" persp-prev "previous" :exit nil)
      ("r" persp-rename "rename")
      ("a" persp-add-buffer "add buffer")
      ("k" persp-remove-buffer "remove buffer")
      ("D" persp-kill "Delete perspective"))
    ($leader-set-key
      "l" 'persp/body))
#+END_SRC

*** eyebrowse
minimal window managment package.
#+BEGIN_SRC emacs-lisp
  (use-package eyebrowse
    :init
    (defhydra eyebrowse (:exit t :pre (eyebrowse-mode))
      "Window Config"
      ("e" eyebrowse-switch-to-window-config "switch")
      ("n" eyebrowse-next-window-config "next" :exit nil)
      ("p" eyebrowse-prev-window-config "previous" :exit nil)
      ("d" eyebrowse-close-window-config-prompt "close")
      ("1" eyebrowse-switch-to-window-config-1)
      ("2" eyebrowse-switch-to-window-config-2)
      ("3" eyebrowse-switch-to-window-config-3)
      ("4" eyebrowse-switch-to-window-config-4)
      ("5" eyebrowse-switch-to-window-config-5))
    ($leader-set-key
      "e" 'eyebrowse/body)
    :config
    (general-define-key
     :keymaps 'eyebrowse-mode-map
     "C-c C-w" nil))
#+END_SRC

*** toggles
minor modes that I commonly toggle on and off
#+BEGIN_SRC emacs-lisp
  ($leader-set-key
    "t" '(:ignore t :wk "toggle")
    "tn" 'display-line-numbers-mode
    "tl" 'toggle-truncate-lines
    "te" 'toggle-debug-on-error
    "tq" 'toggle-debug-on-quit
    "tw" 'whitespace-mode
    "tg" 'git-gutter-mode)
#+END_SRC

*** restart
#+BEGIN_SRC emacs-lisp
  (use-package restart-emacs
    :init
    ($leader-set-key
      "qr" 'restart-emacs))
#+END_SRC

changing the volume on my mic triggers these bindings. So we ignore them.
#+BEGIN_SRC emacs-lisp
  (general-define-key
   "<XF86AudioLowerVolume>" 'ignore
   "<XF86AudioRaiseVolume>" 'ignore)
#+END_SRC

** Ivy

*** ivy
I feel like ivy is simpler to setup so I am going to give it a try. I am going
to have to try to fix =counsel-ag= out of order matching if I want to live with
it though.

#+BEGIN_SRC emacs-lisp
  (use-package ivy
    :straight
    (ivy
     :type git :host github :repo "CeleritasCelery/swiper"
     :upstream (:host github
                      :repo "abo-abo/swiper"))
    :delight
    :general
    (:keymaps 'ivy-minibuffer-map
              "C-j" 'ivy-next-line
              "C-k" 'ivy-previous-line
              "C-h" "DEL"
              "C-S-H" help-map
              "C-l" 'ivy-alt-done
              "<C-return>" 'ivy-immediate-done)
    :init
    (setq ivy-height 15
          ivy-use-virtual-buffers t
          ivy-extra-directories nil
          ivy-use-selectable-prompt t
          ivy-re-builders-alist '((t . ivy--regex-ignore-order)))
    ($leader-set-key
      "bg" 'ivy-switch-buffer)
    :config
    (defun ivy-yank-action (x)
      (kill-new x))
    (ivy-set-actions
     t
     '(("y" ivy-yank-action "yank")))
    (ivy-set-actions 'counsel-find-file nil))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (use-package ivy-hydra
    :straight
    (ivy-hydra
     :type git :host github :repo "CeleritasCelery/swiper"
     :upstream (:host github
                      :repo "abo-abo/swiper"))
    :after (ivy hydra))
#+END_SRC

*** swiper
#+BEGIN_SRC emacs-lisp
  (use-package swiper
    :straight
    (swiper
     :files ("swiper.el")
     :type git :host github :repo "CeleritasCelery/swiper"
     :upstream (:host github
                      :repo "abo-abo/swiper"))
    :init
    ($leader-set-key
      "os" 'swiper))
#+END_SRC

*** counsel
#+BEGIN_SRC emacs-lisp
  (use-package counsel
    :straight
    (counsel
     :type git :host github :repo "CeleritasCelery/swiper"
     :upstream (:host github
                      :repo "abo-abo/swiper"))
    :delight)
#+END_SRC

*** rich foratting
This package makes =ivy-switch-buffer= behave more like =helm-mini=
(i.e. displays the buffer type and full path to recentf files). This
is just too slow to use right now though.
#+BEGIN_SRC emacs-lisp :tangle ~/.emacs.d/ignore.el
  (use-package ivy-rich
    :demand t
    :after ivy
    :config
    (setq ivy-virtual-abbreviate 'full
          ivy-rich-switch-buffer-align-virtual-buffer t)
    (ivy-set-display-transformer 'ivy-switch-buffer 'ivy-rich-switch-buffer-transformer))
#+END_SRC

*** smex
smex is an enchanced version of =M-x= that will record history and is
integrated into ivy
#+BEGIN_SRC emacs-lisp
  (use-package smex
    :init
    (setq smex-history-length 32))
#+END_SRC

*** prescient
keeps track of statistics for usage and presents most familiar
candiates first. Currently does not support regex, which makes it much
less useful
#+BEGIN_SRC emacs-lisp
  (use-package ivy-prescient
    :after ivy
    :demand t
    :config
    (ivy-prescient-mode)
    (prescient-persist-mode))
#+END_SRC

** evil
evil is the Extensible VI Layer. It gives us all the power of vim
without the draw back of using vimscript for config.

*** general

because we are using evil collection, we need to disable evil's
builtin integration *before* evil is loaded.
#+BEGIN_SRC emacs-lisp
  (setq evil-want-integration nil)
#+END_SRC

we want to overide most control keybindings to make them behave like
Vim instead of like Emacs.
#+BEGIN_SRC emacs-lisp
  (use-package evil
    :demand t
    :init
    (setq evil-jumps-cross-buffers nil
          evil-want-C-u-scroll t
          evil-want-C-d-scroll t
          evil-want-C-w-delete t
          evil-want-C-i-jump t
          evil-want-Y-yank-to-eol t)
    :config
    (general-swap-key nil 'motion "0" "^")
    (evil-mode 1))
#+END_SRC

Using =*= and =#=, search foward for symbols, not words
#+BEGIN_SRC emacs-lisp
  (setq evil-symbol-word-search t)
#+END_SRC

we want to use visual lines, but then the line operators don't work
(i.e. =dj= will not operate on literal lines). So we do some simple
remapping instead of setting =evil-respect-visual-line-mode=.
[[https://github.com/emacs-evil/evil/issues/188][emacs-evil/evil#188]]
#+BEGIN_SRC emacs-lisp
  (general-define-key
       :states 'motion
       [remap evil-next-line] 'evil-next-visual-line
       [remap evil-previous-line] 'evil-previous-visual-line)

  (general-define-key
       :states 'operator
       [remap evil-next-line] 'evil-next-line
       [remap evil-previous-line] 'evil-previous-line)
#+END_SRC

add a little hack to prevent =v$= from grabbing the newline. This is a
much better default, but the evil people don't like it and won't add
an option to support it. Nice thing about Emacs though, is we can do
it anyway.  [[https://github.com/emacs-evil/evil/issues/897][emacs-evil/evil#897]]
#+BEGIN_SRC emacs-lisp
  (defvar evil-v$-gets-eol nil)

  (evil-define-motion evil-end-of-line (count)
    "Move the cursor to the end of the current line. If COUNT is
      given, move COUNT - 1 lines downward first."
    :type inclusive
    (move-end-of-line count)
    (when evil-track-eol
      (setq temporary-goal-column most-positive-fixnum
            this-command 'next-line))
    (unless (and (evil-visual-state-p) evil-v$-gets-eol)
      (evil-adjust-cursor)
      (when (eolp)
        ;; prevent "c$" and "d$" from deleting blank lines
        (setq evil-this-type 'exclusive))))
#+END_SRC

prevent "vimmers" from quiting my Emacs. Old habbits die hard.
#+BEGIN_SRC emacs-lisp
  (evil-ex-define-cmd "q" nil)
  (evil-ex-define-cmd "wq" nil)
#+END_SRC

*** magic searching
by default =evil-ex= uses the emacs regex engine which has some very
weird syntax and annoying escaping. instead we want to use evil's
=very-magic= mode which provdies something much more PCRE
compatible. however we can't just set this directly because
=evil-multiedit= relies on =evil-ex= to be "non-magic". Therefore we
just create user callable wrapper around evil-ex functions. Magic mode
only works if the search module is =evil-search=.
#+BEGIN_SRC emacs-lisp
  (setq evil-search-module 'evil-search
        evil-ex-search-vim-style-regexp t)

  (defmacro $make-magic (cmd)
    `(defun ,(intern (concat "$magic-" (symbol-name cmd))) ()
       (interactive)
       (let ((evil-magic 'very-magic))
         (call-interactively (quote ,cmd)))))


  (general-define-key
   :states '(motion normal visual)
   ":" ($make-magic evil-ex)
   "/" ($make-magic evil-ex-search-forward)
   "?" ($make-magic evil-ex-search-backward)
   "n" ($make-magic evil-ex-search-next)
   "N" ($make-magic evil-ex-search-previous))
#+END_SRC

*** text objects
text objects are areas of the buffer that can be easily selected with
one key. The =evil-indent-plus= pakcage provides =i=, =I=, and =J=
texdt objects the select based on indentation.
#+BEGIN_SRC emacs-lisp
  (with-eval-after-load 'evil
    (evil-define-text-object evil-inner-buffer (count &optional beg end type)
      (list (point-min) (point-max)))

    (evil-define-text-object evil-pasted (count &rest args)
      (list (save-excursion (evil-goto-mark ?\[) (point))
            (save-excursion (1+ (evil-goto-mark ?\])) (point))))

    (evil-define-text-object evil-filename (count &rest args)
      (let ((bounds (bounds-of-thing-at-point 'filename)))
        (list (car bounds) (cdr bounds))))

    (general-define-key
     :keymaps 'evil-inner-text-objects-map
     "g" 'evil-inner-buffer
     "P" 'evil-pasted
     "F" 'evil-filename))

  (use-package evil-indent-plus
    :demand t
    :config
    (evil-indent-plus-default-bindings))
#+END_SRC

*** keybindings
=C-i= can be used to move forward in cursor jumps, but Emacs binds it to =TAB=, so
we rebinding it to =H-i=. Though this won't work in the terminal
#+BEGIN_SRC emacs-lisp
  (general-define-key
   :keymaps 'input-decode-map
   "C-i" "H-i")
  (general-define-key
   :states 'normal
   "H-i" 'evil-jump-forward)
#+END_SRC

We want to hybridize some usefull emacs commands with better evil keybindings
#+BEGIN_SRC emacs-lisp
  (general-define-key
   :states 'insert
   "C-y" 'yank)
#+END_SRC

general leader key bindings
#+BEGIN_SRC emacs-lisp
  ($leader-set-key
    "hde" 'describe-face
    "hdm" 'describe-mode
    "hdc" 'describe-char
    "hs"  'profiler-start
    "hS"  'profiler-stop
    "hr"  'profiler-report
    "hR"  'profiler-reset
    "d" 'save-buffer
    "br" 'rename-buffer
    "bR" 'revert-buffer
    "s" '(:ignore t :wk "search")
    "sc" 'evil-ex-nohighlight
    "u" 'universal-argument)
#+END_SRC

*** undo-tree
#+BEGIN_SRC emacs-lisp
  ($leader-set-key
    "U" 'undo-tree-visualize)
#+END_SRC

*** unimpaired
evil unimpaired binds some usefull functions to some quick keys.
#+BEGIN_SRC emacs-lisp
  (use-package evil-unimpaired
    :straight
    (evil-unimpaired
     :type git :host github :repo "CeleritasCelery/evil-unimpaired"
     :upstream (:host github
                      :repo "zmaas/evil-unimpaired"))
    :defer 2
    :init
    (setq evil-unimpaired-leader-keys '("gk" . "gj"))
    :config
    (evil-unimpaired-mode))
#+END_SRC

*** anzu
provides total number of searches in the modeline
#+BEGIN_SRC emacs-lisp
  (use-package evil-anzu
    :demand t)
#+END_SRC

*** snipe
we only want evil snipe for the ability to repeat =f,F,t,T=. I find
avy is better for the actual sniping
#+BEGIN_SRC emacs-lisp :tangle no
  (use-package evil-snipe
    :demand t
    :init
    (setq evil-snipe-override-evil-repeat-keys nil)
    :config
    (evil-snipe-override-mode)
  (add-to-list 'evil-snipe-disabled-modes 'undo-tree-visualizer-mode))
#+END_SRC

*** vi tilde
this package adds a tilde to the fringe of every line that is
empty. eventually I just want to replace this with a save buffer hook
that removes additional lines at the end of the file.
#+BEGIN_SRC emacs-lisp
  (use-package vi-tilde-fringe
    :delight
    :demand t
    :config
    (global-vi-tilde-fringe-mode))
#+END_SRC

*** escape
#+BEGIN_SRC emacs-lisp
  (use-package evil-escape
    :delight
    :demand t
    :init
    (setq evil-escape-unordered-key-sequence t
          evil-escape-key-sequence "jk")
    :config
    (evil-escape-mode))
#+END_SRC

*** collection
evil collection evilifies several major and minor modes to make them
behave better with evil.
#+BEGIN_SRC emacs-lisp
  (use-package evil-collection
    :demand t
    :config
    (add-hook 'evil-collection-setup-hook
              (defun $unmap-leader (_m keymaps)
                (when keymaps
                  (general-define-key
                   :states 'normal
                   :keymaps keymaps
                   $leader-key nil
                   $mm-leader-key nil))))
    (evil-collection-init '(calc calendar custom debug eldoc
                                 elisp-mode dired help info
                                 integration occur popup profiler
                                 wgrep wdired which-key)))
#+END_SRC

*** surround
#+BEGIN_SRC emacs-lisp
  (use-package evil-surround
    :defer 4
    :config
    (global-evil-surround-mode)
    (general-define-key
     :states 'visual
     :keymaps 'evil-surround-mode-map
     "s" 'evil-surround-region
     "S" 'evil-substitute))
#+END_SRC

*** ediff
#+BEGIN_SRC emacs-lisp
  (use-package evil-ediff
    :commands evil-ediff-startup-hook
    :hook (ediff-startup . evil-ediff-startup-hook)
    :init
    (evil-set-initial-state 'ediff-mode 'motion)
    :config
    (evil-ediff-adjust-help))
#+END_SRC

*** commenting
#+BEGIN_SRC emacs-lisp
  (use-package evil-nerd-commenter
    :commands (evilnc-copy-and-comment-operator
               evilnc-comment-operator)
    :init
    ($leader-set-key
      "k" '(evilnc-comment-operator :wk "comment")
      "K" '(evilnc-copy-and-comment-operator :wk "copy-and-comment")))
#+END_SRC

*** lispy
These packages are great at providing editor shortcuts for editing
lisp. There are several things I want to change however.
- I am starting to think that I would be better off just using the
  evil lisp state, and then binding some of the most convient
  functions from both to the new state. Some of the evil-cleverparens
  functions are smarter then their equivalents in lispyville.
#+BEGIN_SRC emacs-lisp
  (use-package lispy
    :delight
    :hook emacs-lisp-mode
    :init
    (setq lispy-colon-p nil))

  (use-package lispyville
    :delight
    :hook lispy-mode
    :init
    (setq lispyville-key-theme
          '(operators
            c-w
            slurp/barf-cp
            additional
            additional-movement
            additional-insert
            additional-wrap))

    :config
    (eval `(defhydra lispyville-mark (:exit nil)
             ("v" ,(lispyville-wrap-command lispy-mark-symbol visual) "symbol")
             ("V" ,(lispyville-wrap-command lispy-mark visual) "sexp")))
    ($leader-set-key
      "v" 'lispyville-mark/lispyville-wrap-lispy-mark-visual))
#+END_SRC

** Helm
helm is the more powerfull of the two between itself and ivy. I really
want to give Ivy a good try because it seems snappier on large files,
but there are still some issues that gives helm the advantage
- =helm-ag= supports out of order matching
- helm doesn't break when using "regex" characters in pattern
- helm is integrated with dired.
- helm file sorting is more sane
- helm-mini has better recentf functionality
- helm supports frames, which makes swoop easier
- =counsel-fzf= is broken, but =helm-fzf= works great
- helm supports marking, which is very efficent.

*** general
my biggest beef with helm is that when using tramp it very slow. I
will have to look into seeing what can be done about that. But the
code is so intimidating. But thankfully I only use tramp about once a
week to copy some files, so it is not a big deal.

I am using helm-adaptive, but it only seems to work for helm-bookmark,
and some grep functions. I want to create an adaptive sort of the
describe functions and one for helm find files. I will have to see how
hard that is to do. I am trying to create something similar to
ivy-precient without all the limitations. Also by creating my own
describe functions, I allow helpful and still have a persistent
action.
#+BEGIN_SRC emacs-lisp
  (use-package helm
    :general
    (:keymaps 'minibuffer-local-map
              "C-c C-l" 'helm-minibuffer-history)
    (general-define-key :keymaps 'helm-map
                        "C-j" 'helm-next-line
                        "C-k" 'helm-previous-line
                        "C-h" 'helm-next-source
                        "C-S-h" 'helm-help
                        "C-c C-h" 'describe-key
                        "C-l" "RET"
                        "C-z" 'helm-select-action
                        "TAB" 'helm-execute-persistent-action)
    (:keymaps '(helm-find-files-map
                helm-read-file-map)
              "C-l" 'helm-execute-persistent-action
              "C-h" 'helm-find-files-up-one-level)
    :init
    ($leader-set-key
      "ff" 'helm-find-files
      "fr" 'helm-recentf
      "r"  'helm-resume
      "bh" 'helm-buffers-list
      "M-x" 'helm-M-x
      "SPC" 'helm-M-x
      "bb" 'helm-mini)
    (setq
     helm-split-window-inside-p t
     helm-always-two-windows t
     helm-buffer-max-length 60
     helm-ff-candidate-number-limit 1000
     helm-display-function '$display-helm-window)

    (defun $display-helm-window (buffer &optional resume)
      (let ((display-buffer-alist
             '(("*.*Helm.*Help.**")
               ("*.*helm.**"
                (display-buffer-in-side-window)
                (inhibit-same-window . t)
                (side . bottom)
                (window-width . 0.6)
                (window-height . 0.4)))))
        (helm-default-display-buffer buffer)))
    :config
    (helm-mode)
    (helm-adaptive-mode)
    (setq helm-adaptive-history-length 100)
    (dired-async-mode)
    (setq helm-ff-delete-files-function 'helm-delete-marked-files-async))
#+END_SRC

*** bookmarks
don't set the org source as the first one in the book marks. Ideally
since I am using helm-adaptive-mode the most commonly used bookmarks
will be first.
#+BEGIN_SRC emacs-lisp
  (use-package helm-bookmark
    :straight nil
    :init
    ($leader-set-key
      "fb" 'helm-filtered-bookmarks)
    :config
    (setq helm-bookmark-default-filtered-sources
          (-insert-at 2 (car helm-bookmark-default-filtered-sources)
                      (cdr helm-bookmark-default-filtered-sources))))

#+END_SRC

*** evil
some hacks to make helm more evil compatible See
[[https://github.com/syl20bnr/spacemacs/issues/3700][syl20bnr/spacemacs#3700]]
#+BEGIN_SRC emacs-lisp
  (defun $helm-unprevent-minibuffer-escape ()
    (when helm-prevent-escaping-from-minibuffer
      (general-define-key :states 'motion
                          [down-mouse-1] 'evil-mouse-drag-region)
      (general-define-key :states 'normal
                          [mouse-2] 'mouse-yank-primary)))

  (defun $helm-prevent-minibuffer-escape ()
    (when helm-prevent-escaping-from-minibuffer
      (general-define-key :states 'motion
                          [down-mouse-1] nil)
      (general-define-key :states 'normal
                          [mouse-2] nil)))

  (defun $helm-hide-cursor-in-buffer ()
    (with-helm-buffer
      (setq cursor-in-non-selected-windows nil)))

  (general-add-hook 'helm-after-initialize-hook
                    '($helm-prevent-minibuffer-escape
                      $helm-hide-cursor-in-buffer))
  (add-hook 'helm-cleanup-hook #'$helm-unprevent-minibuffer-escape)
#+END_SRC

*** minibuffer
sice we are echoing the prompt in the helm buffer there is no reason
to have it in the minibuffer as well.
#+BEGIN_SRC emacs-lisp
  (setq helm-echo-input-in-header-line t)

  (defun $helm-hide-minibuffer-maybe ()
    "Hide minibuffer in Helm session if we use the header line as input field."
    (when (with-helm-buffer helm-echo-input-in-header-line)
      (let ((ov (make-overlay (point-min) (point-max) nil nil t)))
        (overlay-put ov 'window (selected-window))
        (overlay-put ov 'face
                     (let ((bg-color (face-background 'default nil)))
                       `(:background ,bg-color :foreground ,bg-color)))
        (setq-local cursor-type nil))))

  (add-hook 'helm-minibuffer-set-up-hook '$helm-hide-minibuffer-maybe)
#+END_SRC

*** files
set of function to make helm find files more usable.
- remove the parent hardlink from the list (we can always go up a
  directory).
- make sure current directory is not selected by default. rarely am I
  going to just select the current directory. This is especially
  useful when I want to quickly navigate through multiple directories
  with only one entry. I can just use tab over and over again instead
  of having to type.

#+BEGIN_SRC emacs-lisp
  (defun $helm-skip-dots ()
    (let ((cands (helm-marked-candidates))
          (sel   (helm-get-selection)))
      (if (and sel
               (not (cdr cands))
               (file-directory-p sel)
               (string= "." (helm-basename sel)))
          (helm-next-line))))

  (advice-add 'helm-ff-setup-update-hook :after
              (defun $helm-add-skip-dot ()
                (add-hook 'helm-after-update-hook '$helm-skip-dots)))

  (advice-add 'helm-find-files-cleanup :after
              (defun $helm-remove-skip-dot ()
                (remove-hook 'helm-after-update-hook '$helm-skip-dots)))

  (advice-add 'helm-ff-filter-candidate-one-by-one
              :before-while (defun $helm-ff-not-parent-hardlink-p (file)
                              (not (string-suffix-p ".." file))))
#+END_SRC

ignore lockfiles and backups when looking through the filesystem
#+BEGIN_SRC emacs-lisp
  (csetq helm-boring-file-regexp-list
         (list "~" "#" (rx ".#" (1+ nonl))))
  (csetq helm-ff-skip-boring-files t)
#+END_SRC

open file in clipboard. Usually something I copied from an email
#+BEGIN_SRC emacs-lisp
  (defun $open-file-in-clipboard ()
    (interactive)
    (helm-find-files-1 (string-trim (current-kill 0))))
  ($leader-set-key
    "fo" '$open-file-in-clipboard)
#+END_SRC

a better fild-file-at-point function that allows full directory
naviagation
#+BEGIN_SRC emacs-lisp
  (defun $find-file-at-point ()
    "A better replacement for `find-file-at-point' that gives me
  the full power of helm"
    (interactive)
    (let ((file ($get-path-at-point)))
      (helm-find-files-1 (concat (or (file-remote-p default-directory) "")
                                 file)
                         (file-name-base file))))

  (general-define-key
   :states '(normal visual motion)
   :keymaps 'global
   "gf" '$find-file-at-point)
#+END_SRC

*** dispatcher
these are functions that I often want to run, but normally have to
exit helm to call their keybindings. So we create wrapper thats lets
us call them from helm.
#+BEGIN_SRC emacs-lisp
  (defun $helm-ag-from-session ()
    "Launch `helm-ag' from within a helm session"
    (interactive)
    (with-helm-alive-p
      (helm-run-after-exit
       'helm-do-ag
       helm-ff-default-directory
       (let ((cand (helm-marked-candidates)))
         ;; if we have not marked anything we want to search the current directory
         (unless (equal (list (helm-get-selection))
                        cand)
           cand)))))

  (defun $magit-from-helm-session ()
    "run magit from a helm session"
    (interactive)
    (with-helm-alive-p
      (helm-run-after-exit
       '$magit-status-in-dir
       helm-ff-default-directory)))

  (defun $helm-ff-switch-to-shell ()
    "Run switch to shell action from `helm-source-find-files'."
    (interactive)
    (with-helm-alive-p
      (helm-run-after-exit
       '$shell-pop
       helm-current-prefix-arg
       helm-current-buffer
       helm-ff-default-directory)))

  (defun $helm-copy-to-kill-ring ()
    "Copy selection or marked candidates to the kill ring.
  Note that the real values of candidates are copied and not the
  display values.
  If a file name, copy the full path unless C-u prefix is given."
    (interactive)
    (with-helm-alive-p
      (helm-run-after-exit
       (lambda (cands)
         (with-helm-current-buffer
           (kill-new (mapconcat
                      (lambda (c)
                        (format "%s" (if (and (null helm-current-prefix-arg)
                                              (stringp c)
                                              (file-exists-p c))
                                         (file-truename c)
                                       c)))
                      cands "\n"))))
       (helm-marked-candidates))))
#+END_SRC

more convient keybindings for dispatcher functions as well as some helm builtins
#+BEGIN_SRC emacs-lisp
  (with-eval-after-load 'helm
    (general-define-key
     :keymaps 'helm-map
     "C-c y" '$helm-copy-to-kill-ring)
    (general-define-key
     :keymaps '(helm-find-files-map helm-read-file-map helm-generic-files-map)
     "C-r"   'helm-find-files-history
     "C-c m" 'helm-ff-bookmark-set
     "C-c b" 'helm-find-files-toggle-to-bookmark
     "C-c '" '$helm-ff-switch-to-shell
     "C-c s" '$helm-ag-from-session
     "C-c g" '$magit-from-helm-session))
#+END_SRC

*** swoop
#+BEGIN_SRC emacs-lisp
  (use-package helm-swoop
    :ghook ('after-revert-hook 'helm-swoop--clear-cache)
    :init
    (setq helm-swoop-split-with-multiple-windows t
          helm-swoop-speed-or-color t
          helm-swoop-candidate-number-limit 1000
          helm-swoop-pre-input-function (lambda () ""))


    ($leader-set-key
      "s" '(:ignore t :wk "search")
      "ss" 'helm-swoop
      "sS" '$helm-swoop-region-or-symbol))

  (defun $helm-swoop-region-or-symbol ()
    "Call `helm-swoop' with default input."
    (interactive)
    (let ((helm-swoop-pre-input-function
           (lambda ()
             (if (region-active-p)
                 (buffer-substring (region-beginning)
                                   (region-end))
               (or (thing-at-point 'symbol t) "")))))
      (call-interactively 'helm-swoop)))
#+END_SRC

*** ag
use the ag utility to search through files. The pcre package provides
us with PCRE compatible functions. PCRE is more intuitive then emacs
regex
#+BEGIN_SRC emacs-lisp
  (use-package helm-ag
    :init
    ($leader-set-key
      "sf" 'helm-do-ag
      "sF" '$helm-do-ag-region-or-symbol))

  (use-package pcre2el
    :commands reb-change-syntax)

  (defun $helm-do-ag-region-or-symbol (&optional dir)
    "Search with `ag' with a default input."
    (interactive)
    (require 'helm-ag)
    (cl-letf* (((symbol-value 'helm-ag-insert-at-point) 'symbol)
               ;; make thing-at-point choosing the active region first
               ((symbol-function 'this-fn) (symbol-function 'thing-at-point))
               ((symbol-function 'thing-at-point)
                (lambda (thing)
                  (let ((res (if (region-active-p)
                                 (buffer-substring-no-properties
                                  (region-beginning) (region-end))
                               (this-fn thing))))
                    (when res (rxt-quote-pcre res))))))
      (helm-do-ag dir)))
#+END_SRC

*** projectile
this still needs to be setup, as I am currently using counsel
#+BEGIN_SRC emacs-lisp
  (use-package helm-projectile
    :init
    (push '((nil . "helm-projectile") . (nil . "proj")) which-key-replacement-alist)
    ($leader-set-key
      "p" '(:ignore t :wk "project")
      "pp" 'helm-projectile
      "pP" 'helm-projectile-switch-project
      "ps" 'helm-projectile-ag
      "pf" 'helm-projectile-find-file
      "pb" 'helm-projectile-switch-to-buffer
      "pd" 'helm-projectile-find-dir))
#+END_SRC

*** org
use helm to quickly navigate org headings
#+BEGIN_SRC emacs-lisp
  ($leader-local-set-key
    :keymaps 'org-mode-map
    "j" 'helm-org-in-buffer-headings)

  (csetq helm-org-format-outline-path t)
#+END_SRC
*** themes
switch themes using helm
#+BEGIN_SRC emacs-lisp
  (use-package helm-themes
    :init
    ($leader-set-key
      "T" 'helm-themes))
#+END_SRC

*** ediff
running ediff from helm is very convient. But the problem is that is
not reproducible. You have to reselect the files everytime, which is
time consuming. So we create a function to save the last ediff.
#+BEGIN_SRC emacs-lisp
  (defvar $ediff-targets nil
    "The last two files that were diffed")
  (defun $save-ediff-targets (&rest args)
    "Save the last two ediffed files"
    (setq $ediff-targets (car args)))
  (advice-add 'ediff-files-internal :filter-args #'$save-ediff-targets)

  (defun $run-last-ediff ()
    "Run ediff with the last used files"
    (interactive)
    (apply 'ediff-files-internal $ediff-targets))
  ($leader-set-key "fd" '$run-last-ediff)
#+END_SRC

** editing
*** general
make interprogram paste work correctly. don't update the primary when
in evil
#+BEGIN_SRC emacs-lisp
  (setq interprogram-paste-function 'x-cut-buffer-or-selection-value
        evil-kill-on-visual-paste nil)
  (fset 'evil-visual-update-x-selection 'ignore)
#+END_SRC

general editing configuration. We only want to use tabs in specific major modes
#+BEGIN_SRC emacs-lisp
  (setq-default indent-tabs-mode nil)
#+END_SRC

*always* add a final newline. some really poor languages (looking at
you tcsh) require a final newline or the last line of a script never
gets executed.
#+BEGIN_SRC emacs-lisp
  (setq-default require-final-newline t)
#+END_SRC

my filesystem generates regular backups, so having Emacs create
backups is redundant.
#+BEGIN_SRC emacs-lisp
  (setq make-backup-files nil)
#+END_SRC

don't show me long lines in whitespace mode
#+BEGIN_SRC emacs-lisp
  (delq 'lines whitespace-style)
#+END_SRC
*** keybindings
#+BEGIN_SRC emacs-lisp
  ($leader-set-key
    "xa" 'align
    "xr" 'align-regexp
    "xt" 'untabify
    "xd" 'delete-trailing-whitespace)
#+END_SRC

*** narrowing
#+BEGIN_SRC emacs-lisp
  ($leader-set-key
    "n" '(:ignore t :wk "narrow")
    "nw" 'widen
    "nr" 'narrow-to-region
    "nf" 'narrow-to-defun)
#+END_SRC
*** move where I mean
move me to the start of the line or start of code, based on heuristics
#+BEGIN_SRC emacs-lisp
  (use-package mwim
    :general
    (:states 'insert
             "C-e" 'mwim-end
             "C-a" 'mwim-beginning))
#+END_SRC

*** avy
avy is an awesome jump to point package.
#+BEGIN_SRC emacs-lisp
  (use-package avy
    :init
    (setq avy-timeout-seconds 0.3)
    :general (:states 'motion
                      ";" 'avy-goto-char-timer)
    :config
    (evil-collection-init 'avy))
#+END_SRC

*** easy motion
a evil motion package. by default the =F,f,T,t= bindings have buffer
scope, but that makes it too complex, so we will limit that to current
line only
#+BEGIN_SRC emacs-lisp
  (use-package evil-easymotion
    :general
    (:states '(normal visual)
             "s" '(:keymap evilem-map)))
#+END_SRC

*** yasnippet
provides snippets for adding complex blocks. use it with =M-/=
#+BEGIN_SRC emacs-lisp
  (use-package yasnippet
    :diminish
    :defer 3
    :config
    (let ((inhibit-message t))
      (yas-global-mode)))
  (use-package yasnippet-snippets
    :diminish
    :after yasnippet)
#+END_SRC

*** projectile
#+BEGIN_SRC emacs-lisp
  (use-package projectile
    :defer 2
    :init
    (setq projectile-enable-caching t)
    :config
    (projectile-global-mode))
#+END_SRC

*** multiedit
mutliedit is a hybrid of evil-iedit-state and evil-mc.

#+BEGIN_SRC emacs-lisp
  (use-package evil-multiedit
    :init
    (general-define-key
     :states 'visual
     "R" 'evil-multiedit-match-all
     "M-d" 'evil-multiedit-match-and-next
     "M-D" 'evil-multiedit-match-and-prev
     "C-M-D" 'evil-multiedit-restore)
    (general-define-key
     :states 'normal
     "M-d" 'evil-multiedit-match-symbol-and-next
     "M-D" 'evil-multiedit-match-symbol-and-prev)
    (general-define-key
     :states 'insert
     "M-d" 'evil-multiedit-toggle-marker-here)
    (general-define-key
     :states 'motion
     "RET" 'evil-multiedit-toggle-or-restrict-region)
    (general-define-key
     :keymaps 'evil-multiedit-state-map
     "RET" 'evil-multiedit-toggle-or-restrict-region)
    (general-define-key
     :keymaps '(evil-multiedit-state-map
                evil-multiedit-insert-state-map)
     "C-n" 'evil-multiedit-next
     "C-p" 'evil-multiedit-prev))
#+END_SRC

*** hex decimal conversion

#+BEGIN_SRC emacs-lisp
  (defun $radix-name (radix)
    (or (nth radix '("NA" "Unary" "Binary"
                     "Ternary" "Quaternary" "Quinary"
                     "Senary" "Heptary" "Octal"
                     "Nonary" "Decimal" "Undecimal"
                     "Duodecimal" "Tridecimal" "Tetradecimal"
                     "Pentadecimal" "Hexadecimal"))
        (format "(base %d)" radix)))

  (defun $convert-radix-internal (str old-radix new-radix)
    "internal function to convert between two radices"
    (require 'calc-ext) ;; big numbers
    (message "%s %s = %s %s"
             ($radix-name old-radix)
             str
             ($radix-name new-radix)
             (let ((calc-number-radix new-radix))
               (kill-new (downcase (math-format-radix (string-to-number str old-radix)))))))

  (defun $convert-hex-binary ()
    "Converts hex to binary or vice versa and copies the results to the kill ring"
    (interactive)
    (let* ((str (apply 'buffer-substring-no-properties
                       (--map (save-excursion
                                (funcall it "[:xdigit:]#'hx")
                                (point))
                              '(skip-chars-backward skip-chars-forward))))
           (radix (--if-let (car (s-match (rx bos (or (and (0+ (any digit)) "'" (any "hb"))
                                                      (and (any "0#") (any "bx"))))
                                          str))
                      (progn (setq str (s-chop-prefix it str))
                             (if (s-contains? "b" it) 'bin 'hex))
                    'bin)))
      (apply '$convert-radix-internal str (if (eq radix 'bin) '(2 16) '(16 2)))))

  (defun $convert-radix (r1 r2)
    "Convert one radix to another and copy the result to the kill ring"
    (interactive "ncurrent radix: \nndesired radix: ")
    (let* ((valid-chars (if (<= r1 10)
                            (format "0-%d" (- r1 1))
                          (let ((max-char (cond
                                           ((eql r1 11) "a")
                                           ((eql r1 12) "b")
                                           ((eql r1 13) "c")
                                           ((eql r1 14) "d")
                                           ((eql r1 15) "e")
                                           ((eql r1 16) "f"))))
                            (format "0-9a-%sA-%s" max-char (upcase max-char)))))
           (str (apply 'buffer-substring-no-properties
                       (--map (save-excursion
                                (funcall it valid-chars)
                                (point))
                              '(skip-chars-backward skip-chars-forward)))))
      ($convert-radix-internal str r1 r2)))

  ($leader-set-key
    "xc" '$convert-radix
    "xh" '$convert-hex-binary)

#+END_SRC

** files
*** functions
collection of functions stolen from spacemacs
#+BEGIN_SRC emacs-lisp
  (defun $copy-file ()
    (interactive)
    (let* ((destination (read-file-name "Write File: "))
           (dir (file-name-directory destination)))
      (unless (file-exists-p dir)
        (make-directory dir 'parents))
      (write-file destination 'confirm)))

  (defun $delete-file ()
    (interactive)
    (delete-file (buffer-file-name)))

  (defun $rename-file ()
    (interactive)
    (let* ((destination (read-file-name "Rename File: "))
           (dir (file-name-directory destination)))
      (unless (file-exists-p dir)
        (make-directory dir 'parents))
      (rename-file (buffer-file-name) destination)))

  (defun $find-user-config-file ()
    "Edit the org file we use for config, in the current window."
    (interactive)
    (find-file-existing (expand-file-name "emacs.org" user-emacs-directory)))

  (defun $kill-this-buffer ()
    "kill this buffer"
    (interactive)
    (kill-buffer (current-buffer)))

  ($leader-set-key
    "f" '(:ignore t :wk "files")
    "fe" '$find-user-config-file
    "fc" '$copy-file
    "bd" '$kill-this-buffer
    "fD" '$delete-file
    "fR" '$rename-file)
#+END_SRC

*** recentf
#+BEGIN_SRC emacs-lisp
  (use-package recentf
    :init
    (setq recentf-max-saved-items 500))
#+END_SRC

*** ediff
I like to keep everything in one frame. I am not mutli-window heathen
from 2003
#+BEGIN_SRC emacs-lisp
  (setq ediff-window-setup-function 'ediff-setup-windows-plain)
#+END_SRC

** git

don't ask about following symlinks
#+BEGIN_SRC emacs-lisp
     (setq vc-follow-symlinks t)
#+END_SRC

*** magit
magit is the best git porcelain that exists, so far as I can tell. we
need to set the git version to a newer version because the default at
intel is not support. Also the Trash at intel is only 100M so trashing
files can quickly fill up my home disk.
#+BEGIN_SRC emacs-lisp
  (use-package magit
    :init
    (setq magit-git-executable "/usr/intel/pkgs/git/2.12.0/bin/git"
          magit-delete-by-moving-to-trash nil)
    ($leader-set-key
      "g" '(:ignore t :wk "git")
      "gs" 'magit-status
      "gb" 'magit-blame
      "gf" 'magit-file-popup)
    :config
    (general-define-key
     :keymaps 'magit-diff-mode-map
     "SPC" nil))
#+END_SRC

start the commit message editor in insert state
#+BEGIN_SRC emacs-lisp
  (with-eval-after-load 'with-editor
    (add-hook 'with-editor-mode-hook 'evil-insert-state))
#+END_SRC

I don't like to commit to my github repos using my work credentials,
but I can't use my private credentials on my work repos. therefore I
set ~useconfigonly = true~ so that I have to set the credentials for
every repo. I can use these functions as short hand.
#+BEGIN_SRC emacs-lisp
  (defun $git-work-user ()
    "Set my work credentials"
    (interactive)
    (shell-command "git config --local user.name \"Hinckley, Troy J\" &&
     git config --local user.email troy.j.hinckley@intel.com"))

  (defun $git-private-user ()
    "Set my work credentials"
    (interactive)
    (shell-command "git config --local user.name CeleritasCelery &&
     git config --local user.email t.macman@gmail.com"))

  ($leader-set-key
    "gc" '(:ignore t :wk "credentials")
    "gcw" '$git-work-user
    "gcp" '$git-private-user)
#+END_SRC

calling =magit-status= on a whole IP can be very time
consumuing. Often I am just trying to look at a single directory, and
this gives me the power to do that.

#+BEGIN_SRC emacs-lisp
  (defun $magit-status-in-dir (dir)
    "limit magit status to directory"
    (interactive "D")
    (require 'magit)
    (let* ((root (vc-git-root dir))
           (dir (list (file-relative-name dir root)))
           (magit-status-mode-hook (cons (lambda () (setq-local magit-diff-section-file-args dir))
                                         magit-status-mode-hook)))
      (magit-status-internal root)))

  (defun $magit-status-current-dir ()
    "run magit in current dir"
    (interactive)
    ($magit-status-in-dir default-directory))

  (defun $magit-clear-diff-args-if-not-dir-local ()
    (when (--none? (eq (car it) 'magit-diff-section-file-args)
                   dir-local-variables-alist)
      (setq-local magit-diff-section-file-args nil)))

  (defun $magit-status ()
    (interactive)
    (let ((magit-status-mode-hook (cons '$magit-clear-diff-args-if-not-dir-local
                                        magit-status-mode-hook)))
      (call-interactively 'magit-status)))

  ($leader-set-key
    "gd" '$magit-status-current-dir
    "gs" '$magit-status)
#+END_SRC

*** evil
#+BEGIN_SRC emacs-lisp
  (use-package evil-magit
    :demand t
    :after magit
    :config
    (general-define-key
     :keymaps 'magit-mode-map
     "SPC" nil))

  (use-package git-timemachine
    :init
    (evil-collection-init 'git-timemachine)
    ($leader-set-key
      "gt" 'git-timemachine))
#+END_SRC

*** git gutter
git gutter uses the the margin to display the git status. the frige
package uses the fringe instead of the margin. This means that it
won't conflict with linum-mode. However since I don't need linum mode
I can probably remove git gutter fringe all together. All the that it
would offer me is that I could display of the left side, which I may
want to do when I enable flycheck.
#+BEGIN_SRC emacs-lisp
  (use-package git-gutter
    :defer 3
    :init
    (setq git-gutter:diff-option "-w")
    (defhydra $git-hunk (:exit nil)
      ("n" git-gutter:next-hunk "next")
      ("p" git-gutter:previous-hunk "prev")
      ("r" git-gutter:revert-hunk "revert")
      ("h" $git-gutter-show-hunk "show hunk" :exit t)
      ("s" git-gutter:stage-hunk "stage")
      ("v" git-gutter:mark-hunk "mark" :exit t))
    ($leader-set-key
      "gg" '$git-hunk/body)
    :config
    (global-git-gutter-mode))

  (use-package git-gutter-fringe
    :diminish
    :demand t
    :after git-gutter)
#+END_SRC

these changes the behavior of the git gutter popup so that I can quit
with =q= and the window is selected.
#+BEGIN_SRC emacs-lisp
  (advice-add 'git-gutter:update-popuped-buffer :after
              (defun $git-gutter-window-quit (&rest _)
                (when-let ((buffer (get-buffer git-gutter:popup-buffer)))
                  (with-current-buffer buffer
                    (general-define-key
                     :keymaps 'local
                     :states 'normal
                     "q" 'quit-window)))))

  (defun $git-gutter-show-hunk ()
    (interactive)
    (git-gutter:popup-hunk)
    (pop-to-buffer git-gutter:popup-buffer))
#+END_SRC

** shell
*** comint
comint is the generic backend for REPL's and shells. We are giving it
more bash-it style behavior where I can enter some text and then use
the previous command to match against it. Also we can use ivy to
search through the command history. also whenever we try to go to the
previous command it will automatically move us the the command line.
#+BEGIN_SRC emacs-lisp
  (use-package comint
    :straight nil
    :general
    (:keymaps 'comint-mode-map
              "C-k" 'comint-previous-matching-input-from-input
              "C-j" 'comint-next-matching-input-from-input
              "C-S-k" 'comint-previous-prompt
              "C-S-j" 'comint-next-prompt
              [remap comint-dynamic-list-input-ring] 'helm-comint-input-ring)
    :init
    (setq comint-scroll-to-bottom-on-input t
          comint-process-echoes t
          comint-prompt-read-only t))

  (defun $goto-cmd-line (&rest _)
    (goto-char (point-max)))

  (general-advice-add '(comint-next-matching-input-from-input
                        comint-previous-matching-input-from-input) :before '$goto-cmd-line)
#+END_SRC

*** shell
these are utility functions used to setting up the Emacs shell.
#+BEGIN_SRC emacs-lisp
  (use-package shell
    :straight nil
    :gfhook company-mode)

  (defvar $dir-history nil
    "previous shell directories")
  (make-variable-buffer-local '$dir-history)

  (defun $select-shell-history ()
    (interactive)
    (goto-char (point-max))
    (insert (concat "cd " (string-remove-prefix (or (file-remote-p default-directory) "")
                                                (completing-read "directory:" $dir-history)))))

  (general-define-key
   :keymaps 'shell-mode-map
   "C-c C-j" '$select-shell-history)

  (defun $track-shell-directory (str)
    "use the proc filesytem to get the current directory.
    Works on remote shells as well if `shx' and `shx-cmd-set-pid' are used. The
    remote shell will need to echo it's PID in the rc file in the form of `shx'
    markup."
    (when (string-match comint-prompt-regexp str)
      (when-let ((remote  (or (file-remote-p default-directory) ""))
                 (dir (-some->> (local-env-get-shell-pid)
                             (format "/proc/%s/cwd")
                             (concat remote)
                             file-symlink-p)))
        (cd (concat remote dir))
        (unless (equal dir (car $dir-history))
          (push dir $dir-history))))
    str)

  (defun $supress-hostkey-warning (str)
    "EC machines issue a benign but really annoying warning that the EC people
      don't have the technical competence to fix. It has the following form:

      add_host_to_hostkeys: failed to open <missing path> - reason Permission denied "
    (if (string-match "add_host_to_hostkeys: failed to open" str) "" str))

  (defun $shell-mode-hook ()
    (shell-dirtrack-mode 0)
    (setq-local comint-prompt-regexp (rx bol "╰─→ " eos))
    (modify-syntax-entry ?= ".")
    (modify-syntax-entry ?> ".")
    (modify-syntax-entry ?> ".")
    (local-env-mode)
    (setq-local company-backends '(company-async-files
                                   (company-command company-env)
                                   (company-capf company-dabbrev-code)))
    (general-add-hook 'comint-preoutput-filter-functions
                      '($supress-hostkey-warning $track-shell-directory) nil 'local))
  (add-hook 'shell-mode-hook '$shell-mode-hook)
#+END_SRC

generic wrapper function that can be called from other code. currently
used in helm
#+BEGIN_SRC emacs-lisp
  (defun $shell-pop (arg buffer dir)
    "shell-pop to current buffers directory or dir"
    (if (and (boundp 'shell-pop-last-shell-buffer-name)
             (equal (buffer-name buffer)
                    shell-pop-last-shell-buffer-name))
        (shell-pop--cd-to-cwd dir)
      (let ((default-directory dir))
        (shell-pop arg))))
#+END_SRC

I want =C-d= to scroll, not delete a character
#+BEGIN_SRC emacs-lisp
  (when evil-want-C-d-scroll
    (general-define-key
     :keymaps 'comint-mode-map
     "C-d" nil))
#+END_SRC
*** shx
shx mode allows us to call emacs lisp functions from within the
builtin shell, gives us the best of both worlds.
#+BEGIN_SRC emacs-lisp
  (use-package shx
    :diminish
    :hook shell-mode
    :general
    (:keymaps 'shx-mode-map
              "C-<return>" '$shx-send-input-or-copy-path))

  (defun $get-path-at-point ()
    (thread-last
        (apply 'buffer-substring-no-properties
               (--map (save-excursion
                        (funcall it "-{}[:alnum:]$/._~\"")
                        (point))
                      '(skip-chars-backward skip-chars-forward)))
      (replace-regexp-in-string "\"" "")
      (replace-regexp-in-string "/+" "/")
      (replace-regexp-in-string "$ENV" "$")))

  (defun $shx-send-input-or-copy-path ()
    (interactive)
    (if (shx-point-on-input-p)
        (shx-send-input)
      (let ((path ($get-path-at-point)))
        (goto-char (point-max))
        (insert path))))
#+END_SRC

*** exec path from shell
the =EMACS_CAPTURE_ENV= is used as part of my tcshrc file to source
the proper env scripts so that all these variables are set
#+BEGIN_SRC emacs-lisp :tangle no
  (use-package exec-path-from-shell
    :defer 6
    :init
    (setq exec-path-from-shell-check-startup-files nil)
    :config
    (exec-path-from-shell-initialize)

    (let ((exec-path-from-shell-shell-name "tcsh")
          (exec-path-from-shell-arguments nil))
      (setenv "EMACS_CAPTURE_ENV" "1")
      (exec-path-from-shell-copy-envs '("IP_MODELS"
                                        "IP_RELEASES"
                                        "GIT_REPOS"
                                        "RTLMODELS"
                                        "RTL_PROJ_TOOLS"
                                        "RTL_PROJ_CFG"
                                        "RTL_PROJ_BIN"
                                        "RTL_CAD_ROOT"
                                        "GLOBAL_TOOLS"
                                        "CFG_PROJECT"
                                        "VALID_ROOT"
                                        "VTLIB"))
      (setenv "EMACS_CAPTURE_ENV" nil)))
#+END_SRC

*** shell pop
#+BEGIN_SRC emacs-lisp
  (use-package shell-pop
    :init
    ($leader-set-key
      "'" 'shell-pop))
#+END_SRC

only send a cd command if we are not already in that directory
#+BEGIN_SRC emacs-lisp
  (advice-add 'shell-pop--cd-to-cwd
              :before-until
              (defun $shell-in-cwd-p (cwd)
                (file-equal-p default-directory cwd)))
#+END_SRC

my current bash prompt is 2 lines, so we always scroll one line down
so that I can see the whole thing. probably need to make this code
smarter
#+BEGIN_SRC emacs-lisp
  (advice-add 'shell-pop--cd-to-cwd
              :after
              (defun $scroll-shell-pop (_)
                (scroll-down 1)))
#+END_SRC

fix [[https://github.com/kyagi/shell-pop-el/issues/51][kyagi/shell-pop-el#51]]
#+BEGIN_SRC emacs-lisp
  (add-to-list 'shackle-rules `(,(rx "*shell*") :regexp t :same t))
#+END_SRC

*** async file completion
I wrote this package to do async file completion. This is especially
true when using this in a shell, where you don't want to block the ui
while typing.
#+BEGIN_SRC emacs-lisp
  (use-package company-async-files
    :after company
    :straight
    (company-async-files
     :type git :host github :repo "CeleritasCelery/company-async-files")
    :init
    (add-to-list 'company-backends 'company-async-files)
    (delete 'company-files company-backends))
#+END_SRC

*** fish completion
this is a package that I wrote to fallback on fish shell for
completion candidates. I think at some point I would like to
reimplement that argument parsing functionality in lisp and remove the
dependacey on fish.
#+BEGIN_SRC emacs-lisp :tangle ~/.emacs.d/ignore.el
  (use-package company-fish
    :after company
    :straight
    (company-fish
     :type git :host github :repo "CeleritasCelery/company-fish")
    :init
    (add-to-list 'company-backends 'company-fish))
#+END_SRC

*** local env
These packages will be loaded by shell-mode. They let me share the
environment of my local process with the buffer. very useful.
#+BEGIN_SRC emacs-lisp
  (use-package local-env
    :straight
    (local-env :type git
               :host github
               :repo "CeleritasCelery/local-env-mode"
               :files ("local-env.el"))
    :init
    (setq local-env-capture-variables '("MODEL_ROOT"
                                        "STF_SPFSPEC"
                                        "TAP_SPFSPEC"
                                        "TAP2STF_MAP_FILE"
                                        "XWEAVE"
                                        "XWEAVE_REPO_ROOT"
                                        "SPF_ROOT"
                                        "SPF_PERL_LIB"
                                        "DFT_REPO_ROOT"))
    ($leader-local-set-key
      :keymaps 'shell-mode-map
      "e" 'local-env-capture))

  (use-package company-env
    :straight
    (company-env :type git
                 :host github
                 :repo "CeleritasCelery/local-env-mode"
                 :files ("company-env.el")))

  (use-package company-command
    :straight
    (company-command :type git
                     :host github
                     :repo "CeleritasCelery/local-env-mode"
                     :files ("company-command.el")))
#+END_SRC

** compilation
packages that assist in compiling and building code

compilation mode will throw these warnings when clearing large buffers
so we supress them
#+BEGIN_SRC emacs-lisp
  (setq warning-suppress-types '(undo discard-info))
#+END_SRC

*** compile
#+BEGIN_SRC emacs-lisp
  (use-package compile
    :straight nil
    :init
    (add-to-list 'shackle-rules '(compilation-mode :select t))
    (add-to-list 'shackle-rules '(comint-mode :select t))
    (add-to-list 'shackle-rules '(".* ipgen compilation\\*" :regexp t :select t))
    ($leader-set-key
      "o" '(:ignore t :wk "compile")
      "oc" 'compile)
    :config
    (evil-collection-init 'compile)
    (general-define-key
     :keymaps 'compilation-mode-map
     "SPC" nil)
    (font-lock-add-keywords 'compilation-mode
                            `((,(rx bol (0+ space)
                                    (group "#" (0+ nonl)))
                               1 'font-lock-comment-face)
                              (,(rx bol
                                    (group-n 2
                                             (group-n 1 (any "*="))
                                             (1+ (backref 1)))
                                    eol)
                               2 'font-lock-comment-face)
                              (,(rx bol (0+ space)
                                    (group (1+ "-"))
                                    eol)
                               1 'font-lock-comment-face)))
    (add-hook 'compilation-filter-hook
              (defun $compilation-apply-ansi-color ()
                (ansi-color-apply-on-region compilation-filter-start (point)))))
#+END_SRC

compillation can either be run in =compilation-mode= or in
=comint-mode= with =compilation-shell-minor-mode= enabled. The later
is needed when we want to interact with the running process. This has
a few issues that we are going to resolve now:

1. There is a weird issue where the recompile buffer is not the same
   as the original buffer. Therefore we force the buffer name to remain the same by overriding the =compilation-arguments=.

2. It is really convient to dismiss the compilation window with =q= so
   we are going to bind that. regular compilation-mode supports this
   binding by default.

3. We want to start in normal state, so we easily dismiss the
   window with =q= right when it first pops up if we want.
#+BEGIN_SRC emacs-lisp
  (general-define-key
   :definer 'minor-mode
   :states 'normal
   :keymaps 'compilation-shell-minor-mode
   "q" 'quit-window
   "r" 'recompile)
  (general-define-key
   :states 'motion
   :keymaps 'compilation-mode-map
   "r" 'recompile)

  (advice-add 'recompile :before (defun $set-recompile-buffer-name (_)
                                   (setf (nth 2 compilation-arguments) (lambda (_) (buffer-name)))))

  (add-hook 'compilation-shell-minor-mode-hook 'evil-normal-state)
#+END_SRC

    (let* ((model-root (vc-git-root default-directory))
           (ipgen (f-join model-root "tools/ipgen/"))
           (dut (completing-read "select DUT: "
                                 (-map 'f-filename
                                       (f--directories ipgen (member "setup" (directory-files it))))))
           (default-directory (f-join ipgen dut))
           (compilation-environment (list (concat "MODEL_ROOT=" model-root)))
           (compilation-buffer-name-function
            (lambda (mm)
              (concat "*" (f-filename model-root) " " dut " ipgen "  (downcase mm) "*"))))
      (compile (concat "source setup && $DFT_REPO_ROOT/DFTNetworkGen/run_dft_ipgen"
                       (if arg " -B" "") " && touch ./post_ipgen && python ./post_ipgen"))))

  (defhydra bman-cmd (:exit t :columns 2)
    "run a bman command"
    ("a" ($run-bman "") "all")
    ("l" ($run-bman "-s all +s lintra") "lintra")
    ("v" ($run-bman "-s all +s vcs") "vcs")
    ("e" ($run-bman "-s all +s espfmodel") "espf")
    ("c" ($run-bman "-s all +s sglp") "spyglass compile")
    ("s" ($run-bman "-s all +s sgdft") "spyglass")
    ("r" ($run-bman "-s emu -s sgdft -s sglp -s sglp_legacy -s lintra_ol -s FLG -s hip_listgen -s lintra") "RTL only"))

  (defun $run-bman (filter)
    "run dft ipgen in the current model"
    (let* ((model-root (file-truename (vc-git-root default-directory)))
           (compilation-environment (list (concat "MODEL_ROOT=" model-root)))
           (compilation-buffer-name-function
            (lambda (mm)
              (concat "*" (f-filename model-root) " bman " (downcase mm) "*"))))
      (compile (concat "source /p/hdk/rtl/hdk.rc -cfg shdk74 && bman -dut mdf_10nm " filter))))

  ($leader-set-key
    "C" '(:ignore t :wk "compile")
    "Cb" 'bman-cmd/body
    "Ci" '$run-ipgen
    "Cc" 'compile)
#+END_SRC

*** utility
we are doing two things here to make compliation more convient

1. we want to compile with tcsh because that is the standard login shell
for EC machines and some of the compilation setup scripts require it,
we could make it work with bash, but this is easier.

2. by setting the compliation root, we can ensure that we are only
prompted to save buffers that actaully exist in the project instead of
it trying prompt us to save all buffers.
#+BEGIN_SRC emacs-lisp
  (with-eval-after-load 'compile
    (defun $compile-with-tcsh (fn &rest args)
      "use tcsh (standard intel shell) for compilation"
      (let ((shell-file-name "tcsh"))
        (apply fn args)))

    (defvar $current-compilation-dir nil
      "root of current compliation")

    (defun $set-compilation-dir (&rest _)
      "set the root of the current compilation"
      (setq $current-compilation-dir default-directory))

    (setq compilation-save-buffers-predicate
          (lambda ()
            (when-let ((root (vc-git-root (buffer-file-name)))
                       (comp-root (vc-git-root $current-compilation-dir)))
              (f-same? comp-root root))))

    (general-advice-add '(compile recompile) :around #'$compile-with-tcsh)
    (general-advice-add '(compile recompile) :before #'$set-compilation-dir))
#+END_SRC

we need to define what errors look like in compilation and log
files. compilation mode has some defaults but they are not really
applicable to us. Also the built in verilog mode tries to add all of
its error regexp's to the alist everytime it is loaded. This results
in a lot of extra processing that we don't want. Therefore we remove
that hook and set the alist back to our canonical version.
#+BEGIN_SRC emacs-lisp
  (defvar $compilation-error-regexp-alist nil
    "The canonical error regexp alist")

  (with-eval-after-load 'compile
    (add-to-list 'compilation-error-regexp-alist-alist
                 `(ipgen-gmake ,(rx bol "gmake" (optional "[1]") ": *** [" (group-n 1 (1+ nonl)) "] Error " digit) 1))
    (add-to-list 'compilation-error-regexp-alist-alist
                 `(core-assembler ,(rx bol "Information: script '" (group-n 1 (1+ (not (any "'"))))
                                       "'\n" (1+ space) "stopped at line " (group-n 2 (1+ digit)) (1+ nonl)) 1 2))
    (add-to-list 'compilation-error-regexp-alist-alist
                 `(connection-error ,(rx bol (optional "-I-:") "Error: " (group-n 3 (1+ nonl))
                                         "\n" (optional "-I-:") "-" (or "E" "F") "-: [CRT-" (1+ digit) "] Error in "
                                         (1+ nonl) " file " (group-n 1 (1+ (not (any space)))) (0+ space)
                                         "\n" (optional "-I-:") " Error at line# " (group-n 2 (1+ digit)) (1+ nonl))
                                    1 2 nil nil nil (3 font-lock-warning-face)))
    (add-to-list 'compilation-error-regexp-alist-alist
                 `(bman-verilog ,(rx bol "-I-:Error-" (1+ nonl)
                                     "\n-I-:" (group-n 1 (1+ (not space))) ", " (group-n 2 (1+ digit))) 1 2))
    (add-to-list 'compilation-error-regexp-alist-alist
                 `(bman-stage ,(rx bol "-" (or "E" "I") "-:" (optional space) "FAILED:"  (1+ nonl) (or ";" ":") " LOG " (optional ": ") (group (1+ (not space)))) 1))
    (add-to-list 'compilation-error-regexp-alist-alist
                 `(ipgen-hdl-copy ,(rx bol "-I-:Error-[MPD] Module previously declared"
                                       "\n-I-:  The module was previously declared at: "
                                       "\n-I-:  \"" (group (1+ (not space))) "/" (1+ (not space)) (or ".vs" ".sv") "\",") (1 "%s/dft_ipgen.hdl")))


    (setq $compilation-error-regexp-alist '(ipgen-hdl-copy ipgen-gmake core-assembler connection-error bman-stage bman-verilog))
    (setq compilation-error-regexp-alist $compilation-error-regexp-alist))

  (with-eval-after-load 'verilog-mode
    (remove-hook 'compilation-mode-hook 'verilog-error-regexp-add-emacs)
    (setq compilation-error-regexp-alist $compilation-error-regexp-alist))
#+END_SRC

*** alert
the alert package lets us creat notification for any event we want. In
this case we are intersted in knowing when a compliation finishes or
stalls.
#+BEGIN_SRC emacs-lisp
  (use-package alert
    :init
    (setq alert-default-style 'fringe))

  (add-hook 'compilation-filter-hook
            (defun $complilation-detect-core-assembler-stall ()
              "termiate compilation when core assembler stalls"
              (when (looking-back (rx bol "CoreAssembler> ") (line-beginning-position))
                (alert "Core Assembler stall"
                       :severity 'moderate))))

  (setq compilation-finish-functions
        (list (defun $notify-compile-done (buffer exit-string)
                "notfiy the user that compliation is finished"
                (alert "compliation finished"
                       :severity (if (string-prefix-p "exited abnormally" exit-string)
                                     'high
                                   'normal)))))
#+END_SRC


* Languages
:PROPERTIES:
:VISIBILITY: children
:END:
** General
#+BEGIN_SRC emacs-lisp
  (use-package flycheck)
#+END_SRC


*** company
I don't want to use return for completion because that often
interferces with just regular typing. So i have gotten in the habit of
just using =C-l=
#+BEGIN_SRC emacs-lisp
  (use-package company
    :delight
    :general
    (:keymaps 'company-active-map
              "RET" nil
              [return] nil
              "C-l" 'company-complete-selection)
    :hook '(prog-mode org-mode)
    :init
    (setq company-idle-delay 0.2
          evil-collection-company-use-tng nil)
    (evil-collection-init 'company))

  (use-package company-statistics
    :demand t
    :after company
    :config
    (company-statistics-mode))

  (use-package company-shell
    :after company)
#+END_SRC

** Org
*** todos and general
#+BEGIN_SRC emacs-lisp
  (use-package org
    :straight nil
    :gfhook org-indent-mode $org-truncate-lines
    :init
    ($leader-local-set-key
      :keymaps 'org-mode-map
      "," 'org-edit-special
      "g" '$org-navigate/body)
    (setq org-enforce-todo-dependencies t
          org-todo-keywords '((sequence "TODO(t)" "BLOCK(b@!)" "DOING(g)" "|" "DONE(d)")
                              (sequence "|" "CANCELED(c@)"))
          org-priority-faces '((?A . (:foreground "OrangeRed"))
                               (?B . (:foreground "yellow3"))
                               (?C . (:foreground "ForestGreen"))))
    (defun $org-truncate-lines ()
      (let ((inhibit-message t))
        (toggle-truncate-lines)))
    :config
    (add-to-list 'org-structure-template-alist
                 '("el" "#+BEGIN_SRC emacs-lisp\n?\n#+END_SRC" ""))
    (defhydra $org-navigate ()
      "navigate through org headers"
      ("J" org-next-visible-heading "next heading")
      ("K" org-previous-visible-heading "prev heading")
      ("j" org-forward-element "foward")
      ("k" org-backward-element "back")
      ("h" org-up-element "up")
      ("l" org-down-element "down")
      ("<tab>" org-cycle "cycle")))
#+END_SRC

*** functions
#+BEGIN_SRC emacs-lisp
  (use-package org
    :straight nil
    :config
    (general-advice-add '(org-capture-place-template org-edit-src-code)
                        :around (defun $suppress-delete-other-windows (orig-fn &rest args)
                                  (cl-letf (((symbol-function 'delete-other-windows)
                                             (symbol-function 'ignore)))
                                    (apply orig-fn args))))

    (defun $org-show-current-heading-tidily ()
      "Show next entry, keeping other entries closed."
      (interactive)
      (if (save-excursion (end-of-line) (outline-invisible-p))
          (progn (org-show-entry) (show-children))
        (outline-back-to-heading)
        (unless (and (bolp) (org-on-heading-p))
          (org-up-heading-safe)
          (hide-subtree)
          (error "Boundary reached"))
        (org-overview)
        (org-reveal t)
        (org-show-entry)
        (show-children)))
    (advice-add 'counsel-org-goto :after '$org-show-current-heading-tidily)

    (defun $org-archive-done-tasks ()
      "move tasks that are completed to the archive file."
      (interactive)
      (org-map-entries
       (lambda ()
         (org-archive-subtree)
         (setq org-map-continue-from (outline-previous-heading)))
       "/DONE" 'file)
      (org-map-entries
       (lambda ()
         (org-archive-subtree)
         (setq org-map-continue-from (outline-previous-heading)))
       "/CANCELED" 'file))

    ($leader-local-set-key
      :keymaps 'org-mode-map
      "a" '$org-archive-done-tasks
      "h" '$org-show-current-heading-tidily)

    (defun $org-smart-return ()
      "if in a list return should add a new item. If the item is
  blank, we want to break out of the list and delete the blank
  item"
      (interactive)
      (if (org-at-item-p)
          (if (and (looking-back (rx space) (- (point) 1))
                   (memq (- (point) (org-in-item-p))
                         (number-sequence 1 3)))
              ;; If at a blank item, delete it
              (progn
                (beginning-of-line)
                (kill-line)
                (insert "\n")
                (org-return))
            ;; If at a non-blank item, insert a new item
            (org-return)
            (org-insert-item))
        ;; If not at item, normal return
        (org-return)))
    (general-define-key
     :keymaps 'org-mode-map
     "<ret>" '$org-smart-return
     "RET" '$org-smart-return)

    (defun $org-procrastinate (arg)
      "shedule the selected item for tomrrow, effectivly removing
  it from todays agenda."
      (interactive "P")
      (let ((fn (if (eq major-mode 'org-agenda-mode)
                    'org-agenda-schedule
                  'org-schedule)))
        (funcall fn arg "+1d")))
    ($leader-local-set-key
      :keymaps '(org-mode-map org-agenda-mode-map)
      "s" '$org-procrastinate))
#+END_SRC

*** agenda
#+BEGIN_SRC emacs-lisp
  (use-package org-agenda
    :straight nil
    :init
    (setq org-agenda-todo-ignore-scheduled 'future
          org-agenda-dim-blocked-tasks 'invisible
          org-agenda-files "~/org/.agenda-files"
          org-agenda-custom-commands '(("f" tags-todo "mdf-pvim|snr|gen")
                                       ("d" tags-todo "dev")
                                       ("p" tags-todo "pvim+now")))
    ($leader-set-key
      "a" 'org-agenda))
#+END_SRC

*** clock
#+BEGIN_SRC emacs-lisp
  (use-package org-clock
    :straight nil
    :init
    ($leader-local-set-key
      :keymaps 'org-mode-map
      "i" 'org-clock-in
      "o" 'org-clock-out)
    ($leader-local-set-key
      :keymaps 'org-agenda-mode-map
      "i" 'org-agenda-clock-in
      "o" 'org-agenda-clock-out))
#+END_SRC

*** export
#+BEGIN_SRC emacs-lisp
  (use-package ox
    :straight nil
    :init
    (setq org-export-with-section-numbers nil
          org-export-with-toc nil
          org-export-with-sub-superscripts '{}
          org-export-with-priority t
          org-export-preserve-breaks t
          org-html-postamble nil)
    :config
    (setq org-html-head
          (concat
           "<style type=\"text/css\">\n"
           "<!--/*--><![CDATA[/*><!--*/\n"
           (with-temp-buffer
             (insert-file-contents
              "~/org/org-html-themes/styles/email/css/email.css")
             (buffer-string))
           "/*]]>*/-->\n"
           "</style>\n")))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (use-package htmlize)
#+END_SRC

*** capture
#+BEGIN_SRC emacs-lisp
  (use-package org-capture
    :straight nil
    :init
    ($leader-set-key
      "c" 'org-capture)
    ($leader-local-set-key
      :definer 'minor-mode
      :keymaps 'org-capture-mode
      "e" '$export-org-email)
    :config
    (setq org-default-notes-file (expand-file-name "notes.org" org-directory)
          org-default-email-file (expand-file-name "email.org" org-directory)
          org-default-journal-file (expand-file-name "journal.org" org-directory)
          org-capture-templates
          '(("t" "Todo" entry (file+headline org-default-notes-file "Tasks")
             "* TODO [#B] %?\n %t\n" :empty-lines 1)
            ("l" "Todo Link" entry (file+headline org-default-notes-file "Tasks")
             "* TODO [#B] %?\n %i\n %a" :empty-lines 1)
            ("s" "Scheduled TODO" entry (file+headline org-default-notes-file "Tasks")
             "* TODO [#B] %?\n  SCHEDULED: %^T\n" :empty-lines 1)
            ("T" "Todo from Clipboard" entry (file+headline org-default-notes-file "Tasks")
             "* TODO [#B] %?\n  %c" :empty-lines 1)
            ("n" "Note" entry (file+headline org-default-notes-file "Notes")
             "* %?" :empty-lines 1)
            ("N" "Note with Clipboard" entry (file+headline org-default-notes-file "Notes")
             "* %?\n   %c" :empty-lines 1)
            ("e" "Email" entry (file org-default-email-file)
             "* %?" :empty-lines 1)
            ("j" "Journal" entry (file org-default-journal-file)
             "* %<%a %b %e, %l:%M> -  %?" :empty-lines 1))))

     (defun $export-org-email ()
       "Export the current org email and copy it to the clipboard"
       (interactive)
       (let ((org-export-show-temporary-export-buffer nil))
         (org-html-export-as-html)
         (with-current-buffer "*Org HTML Export*"
           (kill-new (buffer-string)))
         (message "HTML copied to clipboard")))
#+END_SRC

*** src blocks
#+BEGIN_SRC emacs-lisp
  (use-package org-src
    :straight nil
    :init
    (setq org-src-fontify-natively t
          org-src-tab-acts-natively t)
#+END_SRC

*** evil
#+BEGIN_SRC emacs-lisp
  (use-package evil-org
    :delight
    :hook org-mode
    :init (add-hook 'org-insert-heading-hook 'evil-insert-state))

  (use-package evil-org-agenda
    :straight nil
    :commands evil-org-agenda-set-keys
    :hook (org-agenda-mode . evil-org-agenda-set-keys))

  (defun $org-todo-or-evil-t ()
    "change the org todo state or evil's till operator"
    (interactive)
    (if (org-at-heading-p)
        (org-todo)
      (call-interactively 'evil-snipe-t)))

  (general-define-key
   :keymaps 'org-mode-map
   :states 'normal
   "t" '$org-todo-or-evil-t)
#+END_SRC

*** visuals
These packages are used to make org mode look better and take
advantage of unicode
#+BEGIN_SRC emacs-lisp
  (use-package org-bullets
    :hook org-mode)

  (use-package org-fancy-priorities
    :diminish
    :hook org-mode
    :init
    (setq org-fancy-priorities-list '("⬆" "⬅" "⬇" "☕")))
#+END_SRC

*** dispatcher
https://gist.github.com/dfeich/1df4e174d45f05fb5798ca514d28c68a
provide a hydra dispatcher based on context
#+BEGIN_SRC emacs-lisp
  (use-package org
    :straight nil
    :init
    ($leader-local-set-key
      :keymaps 'org-mode-map
      "d" '$context-hydra-launcher)
    :config
    (defun $context-hydra-launcher ()
      "A launcher for hydras based on the current context."
      (interactive)
      (let* ((elem (org-element-context))
             (etype (car elem))
             (type (org-element-property :type elem)))
        (cl-case etype
          (src-block (hydra-babel-helper/body))
          (link (hydra-org-link-helper/body))
          ((table-row table-cell) (hydra-org-table-helper/body) )
          (t (message "No specific hydra for %s/%s" etype type)
             (hydra-org-default/body)))))

  ;;; *** org mode hydras
    (defhydra hydra-org-default (:color pink :hint nil)
      "
  Org default hydra
  _s_ insert src block ref with helm
  _q_ quit
  "
      ("s" helm-lib-babel-insert :color blue)
      ("q" nil :color blue))


    (defhydra hydra-org-link-helper (:color pink :hint nil)
      "
  org link helper
  _i_ backward slurp     _o_ forward slurp    _n_ next link
  _j_ backward barf      _k_ forward barf     _p_ previous link
  _q_ quit
  "
      ("i" org-link-edit-backward-slurp)
      ("o" org-link-edit-forward-slurp)
      ("j" org-link-edit-backward-barf)
      ("k" org-link-edit-forward-barf)
      ("n" org-next-link)
      ("p" org-previous-link)
      ("q" nil :color blue))

    (defhydra hydra-org-table-helper (:color pink :hint nil)
      "
  org table helper
  _r_ recalculate     _w_ wrap region      _c_ toggle coordinates
  _i_ iterate table   _t_ transpose        _D_ toggle debugger
  _B_ iterate buffer  _E_ export table     _d_ edit field
  _e_ eval formula    _s_ sort lines
  _q_ quit
  "
      ("E" org-table-export :color blue)
      ("s" org-table-sort-lines)
      ("d" org-table-edit-field)
      ("e" org-table-eval-formula)
      ("r" org-table-recalculate)
      ("i" org-table-iterate)
      ("B" org-table-iterate-buffer-tables)
      ("w" org-table-wrap-region)
      ("D" org-table-toggle-formula-debugger)
      ("t" org-table-transpose-table-at-point)
      ("c" org-table-toggle-coordinate-overlays :color blue)
      ("q" nil :color blue))

    (defhydra hydra-babel-helper (:color pink :hint nil)
      "
  org babel src block helper functions
  _n_ next       _i_ info           _I_ insert header
  _p_ prev       _c_ check
  _h_ goto head  _E_ expand
  ^ ^            _s_ split
  _q_ quit       _r_ remove result  _e_ examplify region
  "
      ("i" org-babel-view-src-block-info)
      ("I" org-babel-insert-header-arg)
      ("c" org-babel-check-src-block :color blue)
      ("s" org-babel-demarcate-block :color blue)
      ("n" org-babel-next-src-block)
      ("p" org-babel-previous-src-block)
      ("E" org-babel-expand-src-block :color blue)
      ("e" org-babel-examplify-region :color blue)
      ("r" org-babel-remove-result :color blue)
      ("h" org-babel-goto-src-block-head)
      ("q" nil :color blue))

    )
#+END_SRC

** lisp

*** elisp
#+BEGIN_SRC emacs-lisp
  (use-package emacs-lisp
    :straight nil
    :init
    ($leader-local-set-key
      :keymaps 'emacs-lisp-mode-map
      "'" 'ielm
      "eb" 'eval-buffer
      "er" 'eval-region
      "ef" 'eval-defun))
#+END_SRC

never have to worry about tab indenting again.
#+BEGIN_SRC emacs-lisp
  (use-package aggressive-indent
    :hook emacs-lisp-mode)
#+END_SRC

set TRAMP verbosity to warnings and errors only. default is level 3
which sends a message every time we connect to a remote host
https://emacs.stackexchange.com/questions/29286/tramp-unable-to-open-some-files
#+BEGIN_SRC emacs-lisp
  (use-package tramp
    :straight nil
    :init
    (setq tramp-default-method "ssh"
          tramp-default-user "tjhinckl"
          tramp-verbose 4
          helm-tramp-connection-min-time-diff 1
          tramp-inline-compress-start-size 1000000
          tramp-copy-size-limit 1000000)
    :config
    (tramp-set-completion-function "ssh" '((tramp-parse-hosts "~/.ssh2/ssh2_config"))))
#+END_SRC

packages to help manage parens
#+BEGIN_SRC emacs-lisp
  (use-package rainbow-delimiters
    :hook prog-mode)

  (use-package paren
    :straight nil
    :demand t
    :after prog-mode
    :init
    (setq evil-show-paren-range 3)
    (evil-collection-init 'paren)
    :config
    (show-paren-mode))
#+END_SRC

** perl
all the enviroment variables are set so as to make it easier for
flycheck to get a clean compile.
#+BEGIN_SRC emacs-lisp
  (use-package cperl
    :straight nil
    :init
    (setq flycheck-perl-executable "/usr/intel/pkgs/perl/5.14.1/bin/perl"
          flycheck-perl-perlcritic-executable "/usr/intel/pkgs/perl/5.14.1-threads/bin/perlcritic"
          flycheck-perl-include-path '("/p/hdk/cad/spf/latest/lib/perl5" ;; SPF library
                                       "../lib/perl5" ;; DTEG ultiscan
                                       "../../lib/perl5" ;; DTEG STF
                                       "..")) ;; library files need to see the library ¯\_(ツ)_/¯
    (setenv "VALID_ROOT" "/p/hdk/rtl/valid/shdk74")
    (setenv "VTLIB" "/p/hdk/rtl/valid/shdk74/lib")
    (setenv "SPF_PERL_LIB" "/p/hdk/cad/spf/latest/lib/perl5")
    (setenv "GLOBAL_TOOLS" "/nfs/site/proj/dpg/tools")
    (setenv "XWEAVE_REPO_ROOT" "/p/hdk/rtl/ip_releases/shdk74/xweave/v17ww43a")
    (setenv "IDS_HOME" "/p/hdk/rtl/cad/x86-64_linux26/dteg/ideas_shell/0.15.1")

    ;; ISC required variables
    (setenv "RTL_CAD_ROOT" "/p/hdk/rtl/cad/x86-64_linux26")
    (setenv "RTL_PROJ_CFG" "/p/hdk/rtl/proj_tools/proj_cfg")
    (setenv "CFG_PROJECT" "shdk74")
    (setenv "RTL_PROJ_BIN" "/p/hdk/rtl/proj_tools/proj_binx/shdk74/latest")
    (setenv "RTL_PROJ_TOOLS" "/p/hdk/rtl/proj_tools"))
#+END_SRC

** json
add hide-show support to json mode and make comments properly font
locked
#+BEGIN_SRC emacs-lisp
  (use-package json-mode
    :config
    (add-to-list 'hs-special-modes-alist (list 'json-mode (rx (any "{[")) (rx (any "]}")) (rx "/" (any "/*"))))
    (font-lock-add-keywords 'json-mode
                            `((,(rx (group "//" (0+ nonl)) eol) 1 font-lock-comment-face))))
#+END_SRC

we are using the design_report.json as reference for our json coding
standard. This varies from the standard json Emacs json pretty printer in
the following ways:
- add a space before plist seperators
- use empty objects ={}= instead of =null=. null causes the json
  reader to choke
- indentation level is 4

#+BEGIN_SRC emacs-lisp
  (setq json-encoding-default-indentation "    ")

  (advice-add 'json-pretty-print :after
              (defun $json-convert-null-to-emtpy-obj (beg end)
                (save-excursion
                  (goto-char beg)
                  (while (re-search-forward (rx ": null") end 'noerror)
                    (replace-match ": {}")))))


  (el-patch-defun json-encode-alist (alist)
    "Return a JSON representation of ALIST."
    (when json-encoding-object-sort-predicate
      (setq alist
            (sort alist (lambda (a b)
                          (funcall json-encoding-object-sort-predicate
                                   (car a) (car b))))))
    (format "{%s%s}"
            (json-join
             (json--with-indentation
              (mapcar (lambda (cons)
                        (format (if json-encoding-pretty-print
                                    (el-patch-swap "%s%s: %s" "%s%s : %s")
                                  "%s%s:%s")
                                json--encoding-current-indentation
                                (json-encode-key (car cons))
                                (json-encode (cdr cons))))
                      alist))
             json-encoding-separator)
            (if (or (not json-encoding-pretty-print)
                    json-encoding-lisp-style-closings)
                ""
              json--encoding-current-indentation)))
#+END_SRC
** other
collection of major modes that I use for different file types.
#+BEGIN_SRC emacs-lisp
  (use-package major-modes
    :straight
    (major-modes
     :type git :host github :repo "CeleritasCelery/major-modes")
    :init
    ($leader-local-set-key
      :keymaps 'spfspec-mode-map
      "g" 'spfspec-goto-definition))
#+END_SRC
